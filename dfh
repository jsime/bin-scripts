#!/usr/bin/env perl

use strict;
use warnings;

use Term::ExtendedColor qw( fg clear );
Term::ExtendedColor::autoreset(0);

my ($i, %d);
my $t_size = 0;
my $t_used = 0;
my $t_avail = 0;

open(my $df, "df -TP|") || die "Error listing devices: $!";

while (<$df>) {
    next if $_ =~ /^filesystem/oi;
    chomp;

    my @t = split(/\s+/);
    $t[0] = shift(@t) . ' ' . $t[0] if $t[1] =~ /^[A-Z]/o && $t[2] !~ /^\d+$/o;

    $t_size += $t[2];
    $t_used += $t[3];
    $t_avail += $t[4];

    $d{$t[6]} = [ @t ];
}

close($df);

my $p_use = sprintf('%d%%', $t_used / $t_size * 100);

my @k = (["Filesystem","Type","Size","Used","Avail","Use%","Mounted on"]);

push(@k, $d{$_}) for sort keys %d;
push(@k, ['','',$t_size,$t_used,$t_avail,$p_use,'']);

foreach my $d (@k) {
    foreach $i (qw( 2 3 4 )) {
        next unless $d->[$i] =~ /^\d+$/o;
        $d->[$i] =
              $d->[$i] >= 1024**4   ? sprintf("%.2fP", $d->[$i] / 1024**4)
            : $d->[$i] >= 1024**3   ? sprintf("%.2fT", $d->[$i] / 1024**3)
            : $d->[$i] >= 1024**2   ? sprintf("%.2fG", $d->[$i] / 1024**2)
            : $d->[$i] >= 1024      ? sprintf("%.2fM", $d->[$i] / 1024)
            : $d->[$i] == 0         ? ''
            : $d->[$i];
    }
}

my (@s, @ln);

foreach my $i (0..6) {
    my $l = 0;
    foreach my $r (@k) {
        my $l2 = length($r->[$i]);
        $l = $l2 if $l2 > $l;
    }
    push(@s, "%" . ($i =~ /^(0|6)$/os ? "-" : "") . $l . "s");
    push(@ln, '-' x $l);
}

my $spec = join("  ", @s);

unshift(@k, shift(@k), [@ln]);
push(@k, [@ln], pop(@k));

# Header lines
printf("%s${spec}%s\n", fg('steelblue1'), @{shift(@k)}, clear());
printf("%s${spec}%s\n", fg('steelblue'), @{shift(@k)}, clear());

# Mountpoint lines
printf("%s${spec}%s\n", fg(usage_color($_->[5])), @{$_}, clear())
    for map { shift(@k) } 0 .. $#k - 2;

# Footer lines
printf("%s${spec}%s\n", fg('steelblue'), @{shift(@k)}, clear());
printf("%s${spec}%s\n", fg(usage_color($k[0][5])), @{$k[0]}, clear());
print clear();

sub usage_color {
    my ($amt) = @_;

    return 'reset' unless defined $amt && $amt =~ m{^(\d+(?:\.\d+)?)\%?$}o;
    $amt = $1;

    my %c = (
        100 => 'blink red1',
        95  => 'red1',
        90  => 'orangered1',
        85  => 'yellow18',
        80  => 'yellow16',
        75  => 'yellow14',
        70  => 'yellow12',
        65  => 'yellow10',
        60  => 'yellow8',
        50  => 'yellow4',
        0   => 'reset'
    );

    return $c{(grep { $_ <= $amt } sort { $b <=> $a } keys %c)[0]} || 'reset';
}

